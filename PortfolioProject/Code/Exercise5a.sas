/**********************************************************************************

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO CONSTRUCT FAMILY-LEVEL VARIABLES FROM PERSON-LEVEL DATA

              THERE ARE TWO DEFINITIONS OF FAMILY UNIT IN MEPS.
                 1) CPS FAMILY:  ID IS DUID + CPSFAMID.  CORRESPONDING WEIGHT IS FAMWT17C.
                 2) MEPS FAMILY: ID IS DUID + FAMIDYR.   CORRESPONDING WEIGHT IS FAMWT17F.

              THE CPS FAMILY IS USED IN THIS EXERCISE.

INPUT FILE:   /folders/myfolders/MEPS/H201.SAS7BDAT (2017 FY PUF DATA)

*********************************************************************************/;

LIBNAME CDATA '/folders/myfolders/MEPS';

TITLE1 'MIS500 Portfolio Project';
TITLE2 "EXERCISE5A.SAS: CALCULATE FAMILY-LEVEL ESTIMATES";

TITLE3 "SAMPLE DUMP FOR FAMILY IDS";
PROC PRINT DATA=CDATA.H201 (OBS=20);
  VAR PID AGE17X SEX CPSFAMID FAMWT17C FAMIDYR FAMWT17F;
  BY DUID;
RUN;

PROC SORT DATA=CDATA.H201 (KEEP=DUPERSID DUID CPSFAMID FAMWT17C VARSTR VARPSU TOTSLF17 TTLP17X)
              OUT=PERS;
  BY DUID CPSFAMID;
RUN;

DATA PERS2
     FAM (KEEP=DUID CPSFAMID FAMSIZE FAMOOP FAMINC);
 SET PERS;
  BY DUID CPSFAMID;

     LABEL FAMSIZE = '# OF PERSONS PER CPS FAMILY'
           FAMOOP  = 'TOTAL OUT-OF-POCKET EXP (TOTSLF17) PER CPS FAMILY'
           FAMINC  = 'TOTAL INCOME (TTLP17X) PER CPS FAMILY';

     IF FIRST.CPSFAMID THEN DO;
        FAMSIZE = 0 ;
        FAMOOP  = 0 ;
        FAMINC  = 0 ;
     END;

     FAMSIZE + 1        ;
     FAMOOP  + TOTSLF17 ;
     FAMINC  + TTLP17X  ;

     OUTPUT PERS2;
     IF LAST.CPSFAMID THEN OUTPUT FAM;
RUN;
TITLE3 "A SAMPLE DUMP TO CHECK THE CREATION OF THE FAMILY-LEVEL VARIABLES";
PROC PRINT DATA=PERS2 (OBS=20);
  BY DUID CPSFAMID;
RUN;

/*ADD WEIGHT, VARSTR, AND VARPSU TO THE FAMILY-LEVEL ANALYTIC DATA*/

PROC SORT DATA=PERS (WHERE=(FAMWT17C>0)) OUT=FAMWT (KEEP=DUID CPSFAMID FAMWT17C VARSTR VARPSU) NODUPKEY;
  BY DUID CPSFAMID;
RUN;

DATA FAM2;
  MERGE FAM (IN=AA) FAMWT (IN=BB);
  BY DUID CPSFAMID;
  IF AA AND BB;
RUN;

TITLE3 "CPS FAMILY-LEVEL ESTIMATES ON FAMILY SIZE, OUT-OF-POCKET EXP, AND INCOME, 2017";
ods graphics off;
PROC SURVEYMEANS DATA=FAM2 NOBS SUMWGT MEAN STDERR;
	STRATA  VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  FAMWT17C ;
	VAR FAMSIZE FAMOOP FAMINC;
RUN;
PROC PRINTTO;
RUN;
