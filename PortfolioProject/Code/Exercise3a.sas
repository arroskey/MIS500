/**********************************************************************************

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO IDENTIFY PERSONS WITH A CONDITION AND
              CALCULATE ESTIMATES ON USE AND EXPENDITURES FOR PERSONS WITH THE CONDITION

              THE CONDITION USED IN THIS EXERCISE IS DIABETES (ICD10CDX = 'E11')



INPUT FILES:  1) /folders/myfolders/MEPS/H199.SAS7BDAT (2017 CONDITION PUF DATA)
              2) /folders/myfolders/MEPS/H201.SAS7BDAT (2017 FY PUF DATA)

*********************************************************************************/;
ods graphics off;

/* Set the library for the datasets */
LIBNAME CDATA '/folders/myfolders/MEPS';

TITLE1 'MIS500 Portfolio Project';
TITLE2 'EXERCISE3A.SAS: CALCULATE ESTIMATES ON USE AND EXPENDITURES FOR PERSONS WITH A CONDITION (DIABETES)';

/* set the categories for printing */
PROC FORMAT;
  VALUE SEX
     . = 'TOTAL'
     1 = 'MALE'
     2 = 'FEMALE'
       ;

  VALUE YESNO
     . = 'TOTAL'
     1 = 'YES'
     2 = 'NO'
       ;
RUN;


/*1) PULL OUT CONDITIONS WITH DIABETES (ICD10CDX = 'E11') FROM 2017 CONDITION PUF - HC199*/

DATA DIAB;
 SET CDATA.H199;
    IF ICD10CDX IN ('E11');
RUN;

TITLE3 "CHECK ICD10CDX CODES FOR DIABETIC CONDITIONS";
PROC FREQ DATA=DIAB;
  TABLES ICD10CDX / LIST MISSING;
RUN;


/*2) IDENTIFY PERSONS WHO REPORTED DIABETES*/

PROC SORT DATA=DIAB OUT=DIABPERS (KEEP=DUPERSID) NODUPKEY;
  BY DUPERSID;
RUN;


/*3) CREATE A FLAG FOR PERSONS WITH DIABETES IN THE 2017 FY DATA*/

DATA  FY1;
MERGE CDATA.H201 (IN=AA)
      DIABPERS   (IN=BB) ;
   BY DUPERSID;

      LABEL DIABPERS='PERSONS WHO REPORTED DIABETES';
      IF AA AND BB THEN DIABPERS = 1;
                   ELSE DIABPERS = 2;

RUN;
TITLE3 "Supporting crosstabs for the flag variables";
TITLE3 "UNWEIGHTED # OF PERSONS WHO REPORTED DIABETES, 2017";
PROC FREQ DATA=FY1;
  TABLES DIABPERS
         DIABPERS * SEX / LIST MISSING;
  FORMAT SEX      sex.
         DIABPERS yesno.
    ;
RUN;

TITLE3 "WEIGHTED # OF PERSONS WHO REPORTED DIABETES, 2017";
PROC FREQ DATA=FY1;
  TABLES DIABPERS
         DIABPERS * SEX /LIST MISSING;
  WEIGHT PERWT17F ;
  FORMAT SEX      sex.
         DIABPERS yesno.
    ;
RUN;


/*4) CALCULATE ESTIMATES ON USE AND EXPENDITURES FOR PERSONS WHO REPORTED DIABETES*/

ODS GRAPHICS OFF;
ODS LISTING CLOSE;
PROC SURVEYMEANS DATA=FY1 NOBS SUMWGT SUM STD MEAN STDERR;
	STRATA  VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT PERWT17F ;
	DOMAIN DIABPERS('1') SEX*DIABPERS('1');
	VAR TOTEXP17 TOTSLF17 OBTOTV17;
      ods output domain=work.domain_results;
RUN;

ODS LISTING;
TITLE3 "ESTIMATES ON USE AND EXPENDITURES FOR PERSONS WHO REPORTED DIABETES, 2017";
PROC PRINT DATA=work.domain_results (DROP=DOMAINLABEL)  NOOBS LABEL BLANKLINE=3 ;
VAR SEX VARNAME N SUMWGT SUM STDDEV MEAN STDERR;
FORMAT N                      comma6.0
       SUMWGT   SUM    STDDEV comma17.0
       MEAN     STDERR        comma9.2
       DIABPERS               yesno.
       SEX                    sex.
   ;
RUN;

PROC PRINTTO;
RUN;
